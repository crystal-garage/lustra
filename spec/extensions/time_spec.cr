require "../spec_helper"

module IntervalSpec
  class IntervalMigration78392
    include Lustra::Migration

    def change(dir)
      create_table(:interval_table) do |t|
        t.column :interval, :interval, null: true
        t.column :time_in_date, :time, null: true

        t.timestamps
      end
    end
  end

  def self.reinit!
    reinit_migration_manager
    IntervalMigration78392.new.apply
  end

  class IntervalModel
    include Lustra::Model

    primary_key

    self.table = "interval_table"

    column interval : Lustra::Interval?
    column time_in_date : Lustra::TimeInDay?
  end

  describe Lustra::Interval do
    it "be saved into database (and converted to pg interval type)" do
      temporary do
        reinit!

        3.times do |id|
          months = Random.rand(-1000..1000)
          days = Random.rand(-1000..1000)
          microseconds = Random.rand(-10_000_000..10_000_000)

          interval = Lustra::Interval.new(months: months, days: days, microseconds: microseconds)
          IntervalModel.create! id: id, interval: interval

          record = IntervalModel.find! id
          record.should_not be_nil

          if interval = record.interval
            interval.months.should eq months
            interval.days.should eq days
            interval.microseconds.should eq microseconds
          end
        end
      end
    end

    it "be added and substracted to a date" do
      # TimeSpan
      [1.month, 1.day, 1.hour, 1.minute, 1.second].each do |span|
        i = Lustra::Interval.new(span)
        now = Time.local

        (now + i).to_unix.should eq((now + span).to_unix)
        (now - i).to_unix.should eq((now - span).to_unix)
      end

      i = Lustra::Interval.new(months: 1, days: -1, minutes: 12)
      now = Time.local

      (now + i).to_unix.should eq((now + 1.month - 1.day + 12.minute).to_unix)
      (now - i).to_unix.should eq((now - 1.month + 1.day - 12.minute).to_unix)
    end

    it "be used in expression engine" do
      IntervalModel.query.where do
        (created_at - Lustra::Interval.new(months: 1)) > updated_at
      end.to_sql.should eq %(SELECT * FROM "interval_table" WHERE (("created_at" - INTERVAL '1 months') > "updated_at"))
    end

    it "be casted into string" do
      Lustra::Interval.new(months: 1, days: 1).to_sql.to_s.should eq("INTERVAL '1 months 1 days'")
    end
  end

  describe Lustra::TimeInDay do
    it "be parsed" do
      value = 12i64 * 3_600 + 50*60
      Lustra::TimeInDay.parse("12:50").microseconds.should eq(value * 1_000_000)

      Lustra::TimeInDay.parse("12:50:02").microseconds.should eq((value + 2) * 1_000_000)

      wrong_formats = {"a:21", ":32:54", "12345", "0:0:0"}

      wrong_formats.each do |format|
        expect_raises(Exception, /wrong format/i) { Lustra::TimeInDay.parse(format) }
      end
    end

    it "be saved into database and converted" do
      temporary do
        reinit!

        time_in_date = "12:32"
        record = IntervalModel.create! time_in_date: time_in_date

        record.time_in_date.should_not be_nil

        if time_in_date = record.time_in_date
          time_in_date.to_s(show_seconds: false).should eq("12:32")

          record.time_in_date = time_in_date + 12.minutes
          record.save!
        end

        record.reload.time_in_date.to_s.should eq("12:44:00")
      end
    end
  end
end
